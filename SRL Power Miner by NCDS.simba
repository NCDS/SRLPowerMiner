program SRLPowerMiner_by_NCDS;
{$DEFINE SMART}
{$DEFINE DEBUG}
{$DEFINE AUTO_UPDATE}
{$loadlib prosocks}
{$I SRL-6/SRL.Simba}
{$i srl-6/lib/misc/srlplayerform.simba}

const
  // Version #
  scriptVersion = '1.6';

  // User defined constants
  untilAntiban = 5; //minimum minutes between anitban routines (plus random 50%)

  // Object constants
  _minColorCount = 5;
  _objWidth = 15;
  _objHeight = 15;

  // Integer player constants
  _count = 0;
  _direction = 1;
  _oreExp = 2;
  _breakIn = 3;
  _breakFor = 4;

  // String player constants
  _reason = 0;
  _ore = 1;
  _oreTxt = 2;
  _quickKey = 3;


var
  oreDTM, _waitTime, _antibanTime: integer;
  takeBreaks, switchWorlds, debug: boolean;
  data: T2DColorData;

procedure declarePlayers();
var
  i, dir, l: integer;
begin
  players.setup(playerForm.playerNames, playerForm.playerFile);
  currentPlayer := 0;

  //Setup player attributes
  for i := 0 to high(players) do
    with players[i] do
    begin
      integers[_breakIn] := strToInt(playerForm.players[i].settings[1]);  //minutes until break
      integers[_breakFor] := strToInt(playerForm.players[i].settings[2]);  //minutes to break
      integers[_count] := 0;
      strings[_reason] := 'Unknown';

      for dir := 0 to high(playerForm.comboBoxItems[1]) do
        if (playerForm.players[i].settings[9] = playerForm.comboBoxItems[1][dir]) then
          integers[_direction] := dir*90;

      world :=  strToInt(playerForm.players[i].settings[3]);

      takeBreaks := strToBool(playerForm.players[i].settings[4]);
      switchWorlds := strToBool(playerForm.players[i].settings[5]);
      FindMod := strToBool(playerForm.players[i].settings[6]);
      debug := strToBool(playerForm.players[i].settings[7]);

      //incase user forgets to set quick key, set it to 1 by default
      if playerForm.players[i].settings[0] = '0' then
         playerForm.players[i].settings[0] := '1';

      strings[_quickKey] := playerForm.players[i].settings[0];
      strings[_ore] := playerForm.players[i].settings[8];
    end;
end;

procedure initPlayerForm();
begin
  with playerForm do
  begin
    name := 'SRL Power Miner v'+toStr(scriptVersion)+' by NCDS';
    scriptHelpThread := '';
    scriptSettingsPath := '';

    editBoxLabels := ['QuickKey', 'Minutes until Break', '# of Minutes to Break', 'World'];
    editBoxDefaults := [ '0', '60', '15','0'];
    checkBoxLabels := ['Take Breaks', 'Switch Worlds (After Break)', 'Logout if Mod is near', 'Debug'];
    checkBoxDefaults := ['True','True','True', 'False'];

    comboBoxLabels := ['OreType','MMDirection'];
    comboBoxDefaults := ['Iron','North'];

    setLength(comboBoxItems, length(comboBoxLabels));
    comboBoxItems[0] := ['Iron', 'Coal'];
    comboBoxItems[1] := ['North', 'East', 'South', 'West'];
  end;
end;

{$IFDEF AUTO_UPDATE}
//credit to Brandon
function getPageEx(URL: string): string;
var
  S: SSLSocket;
  res: ProMemoryStruct;
begin
  Pro_InitSocket(S, nil, nil, nil, nil);
  Pro_CreateSocket(S, '');
  Pro_SetSSL(S, false, false, true);
  Pro_SetURL(S, URL);
  Pro_DoGetEx(S, res);
  {$IFDEF LAPE}
  SetLength(Result, res.size);
  MemMove(res.memory^, Result[1], res.size);
  {$ELSE}
  Result := res.memory;
  {$ENDIF}
  try
    Pro_FreeSocket(S);
  except
    WriteLn('Unable to Free ' + URL + ' ProSocks Error');
  end;
end;
{$ENDIF}

procedure update;
var
  fileStream: integer;
  masterVersion, localVersion, newFile, newFile_path: string;
begin
  {$IFDEF AUTO_UPDATE}
  writeln('[script] Checking version...');
  localVersion := scriptVersion;
  masterVersion := getPageEx('https://raw.githubusercontent.com/NCDS/SRLPowerMiner/master/Version.txt');
  writeln('masterVersion: '+masterVersion+';');
  writeln('localVersion: '+localVersion+';');
  if trim(masterVersion) > localVersion then
  begin
    writeln('Updating local version...');
    newFile := getPageEx('https://raw.githubusercontent.com/NCDS/SRLPowerMiner/master/SRL%20Power%20Miner%20by%20NCDS.simba');
    newFile_path := scriptPath+'SRLPowerMiner v'+masterVersion+' by NCDS.simba';
    fileStream := rewriteFile(newFile_path, true);
    try
      writeFileString(fileStream, newFile);
    except
      writeln('Failed to update local version.');
      writeln('You may update manually at: https://github.com/NCDS/SRLPowerMiner');
      terminateScript();
    end;
    closeFile(fileStream);
    writeln('Update succesful!');
    writeln('New script found at '+newFile_path+'');
    terminateScript();
  end else
  writeln('Local version matches master, no updates available.');
  {$ENDIF}
end;

function findNormalRandoms(): boolean; //result not used?
begin
  mainScreen.setAngle(MS_ANGLE_HIGH); // Angle keeps ending up lower for some reason, so added this call
  result := claimTicket or exitTreasure;
end;

function getColorData(s: string): T2DColorData;
begin
  case lowercase(s) of
    {'tin': begin

           end;}

    'copper': begin
                players[currentPlayer].strings[_oreTxt] := 'opper ore';
                players[currentPlayer].integers[_oreExp] := 0;
                setLength(result, 2);
                result[0].color := 7446732;
                result[0].tolerance := 9;

                result[1].color := 3369925;
                result[1].tolerance := 12;
              end;

    'iron': begin
              players[currentPlayer].strings[_oreTxt] := 'ron ore';
              players[currentPlayer].integers[_oreExp] := 35;
              setLength(result, 3);
              result[0].color := 2899307; //light
              result[0].tolerance := 7;

              result[1].color := 2306133; //mid
              result[1].tolerance := 7;

              result[2].color := 1911369; //dark
              result[2].tolerance := 8;
            end;

    'coal': begin
              players[currentPlayer].strings[_oreTxt] := 'oal';
              players[currentPlayer].integers[_oreExp] := 50;
              setLength(result, 3);
              result[0].color := 1447960; //Lighter color
              result[0].tolerance := 4;

              result[1].color := 1974304; //mid range color
              result[1].tolerance := 4;

              result[2].color := 1118739; //darkest
              result[2].tolerance := 2;
            end;
  end;
end;

function T2DColorData.findColorSpiral(sBox: TBox): boolean;
var
  i, ii, l: integer;
  cs: TColorSettings; //Place holder for now
begin
  {$IFDEF DEBUG}print('findColorSpiral()', TDebug.HEADER);{$ENDIF}
  result := false;
  l := high(self);

  for i := 0 to l do
    if result then
      break
    else
      result := mainScreen.findObject(mainScreen.cx, mainScreen.cy, [self[i].color],
        [self[i].tolerance], cs, mainScreen.getCenterPoint,_objWidth, _objHeight,
        _minColorCount, [players[currentPlayer].strings[_oreTxt]+' rock'], sBox, MOUSE_LEFT);
  {$IFDEF DEBUG}print('findColorSpiral(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

function antiban(): boolean;
begin
  result := true;//???
  if _antibanTime > getSystemTime() and random(100) > 10 then
    exit;
  {$IFDEF DEBUG}print('antiban()', TDebug.HEADER);{$ENDIF}
  _antibanTime := getSystemTime() + ((untilAntiban+random(round(untilAntiban*0.5)))*60000);
  case random(40) of
    0..19: hoverRandomSkill();
    20..22: boredHuman();
    23..35: randomRClickItem();
    36..39: sleepAndMoveMouse(500+random(1000));
  end;
  {$IFDEF DEBUG}print('antiban(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

procedure initDTM;
begin
    oreDTM := DTMFromString('mbQAAAHicY2VgYIhmYmDIAOJQIPYC4kQgDmNkYPADYncgjgPieCDW0JADqmZCwZIMmIARCwYDAFPwBBQ=');
end;

procedure n_freeDTM;
begin
  freeDTM(oreDTM);
end;

function getWaitTime(skill: integer):integer;
var
  lvl: integer;
begin
  {$IFDEF DEBUG}print('getWaitTime()', TDebug.HEADER);{$ENDIF}
  lvl := tabStats.getSkillLevel(skill);
  if lvl < 1 then
    lvl := 60; //If getSkillLevel fails set 6 second wait by default
  result := round((100 div round(lvl*0.5))*2)*1000;
  {$IFDEF DEBUG}print('getWaitTime() = '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

procedure printProgress;
var
  i, l: integer;
  active: boolean;
  hours: extended;
begin
  l := length(players)-1;
  writeln('$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$');
  for i := 0 to l do
  begin
    active := players[i].isActive;
    writeln('Player: '+players[i].nickname+';');
    writeln('Active: '+toStr(active)+';');
    if not active then
      writeln('Reason: '+players[i].strings[_reason]+';');
    writeln('Time worked: '+toStr(msToTime(players[i].worked.getTime(), TIME_ABBREV))+';');
    writeln('Ore: '+players[i].strings[_ore]+';');
    writeln('Ore mined: '+toStr(players[i].integers[_count])+';');
    hours := players[i].worked.getTime()/(60000*60);
    if hours > 0 then
      writeln('~ p/h: '+toStr(round(players[i].integers[_count]/hours))+';')
    else
      writeln('~ p/h: 0;');
    writeln('Experience gained: '+toStr(players[i].integers[_count]*players[i].integers[_oreExp])+';');
    if hours > 0 then
      writeln('~ p/h: '+toStr(round((players[i].integers[_count]*players[i].integers[_oreExp])/hours))+';')
    else
      writeln('~ p/h: 0;');
    if (l > 0) and (i < l) then
      writeln('~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~');
  end;
  writeln('$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$');
end;

procedure initTerminate;
begin
  addOnTerminate('printProgress');
  addOnTerminate('n_freeDTM');
end;

function initPlayer(): boolean;
begin
  {$IFDEF DEBUG}print('initPlayer()', TDebug.HEADER);{$ENDIF}
  wait(2000+random(200)); //lag :/
  disableSRLDebug := not debug;
  closePopup();
  data := getColorData(players[currentPlayer].strings[_ore]);
  minimap.setAngle(players[currentPlayer].integers[_direction]);
  mainScreen.setAngle(MS_ANGLE_HIGH);
  _waitTime := getWaitTime(SKILL_MINING);
  if tabBackpack.open() then
    findNormalRandoms()
  else
  begin
    {$IFDEF DEBUG}print('Failed to open tabBackpack');{$ENDIF}
    players[currentPlayer].strings[_reason] := 'Failed to open tabBackpack';
    exit(false);
  end;
  result := true;
  {$IFDEF DEBUG}print('initPlayer() = '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

function initScript(): boolean;
begin
  clearDebug();
  update;
  {$IFDEF DEBUG}print('initScript()', TDebug.HEADER);{$ENDIF}
  initPlayerForm();
  runPlayerForm();

  if (not playerForm.isScriptReady) then
    terminateScript();

  declarePlayers();
  setupSRL();
  initDTM;
  initTerminate;

  if not players[currentPlayer].login then
  begin
    {$IFDEF DEBUG}print('initPlayer(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
    exit(false);
  end;

  {$IFDEF DEBUG}
    writeln('Angle degrees: '+tostr(players[currentPlayer].integers[_direction]));
  {$ENDIF}
  result := initPlayer();
  {$IFDEF DEBUG}print('initPlayer(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

function takeBreak(): boolean;
var
  t, tt, h, m, s: integer;
begin
  {$IFDEF DEBUG}print('takeBreak()', TDebug.HEADER);{$ENDIF}
  print('Break time...');
  t := round(getSystemTime() + players[currentPlayer].integers[_breakFor]*60000+(random(players[currentPlayer].integers[_breakFor]*60000)*0.15));
  players[currentPlayer].logout();

  while t > getSystemTime do
  begin
    tt := (t-getSystemTime);
    status('Taking break: '+msToTime(tt, TIME_ABBREV));
  end;
  status('');
  result := players[currentPlayer].login;
  if not result then
    players[currentPlayer].strings[_reason] := 'Failed to login from break.';
  {$IFDEF DEBUG}print('takeBreak(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

function getInvCount(openTab: boolean = false): integer;
var
  slot: integer;
begin
  {$IFDEF DEBUG}print('getInvCount()', TDebug.HEADER);{$ENDIF}
  result := -1;
  if openTab then
    if not tabBackpack.open() then
      exit;
  result := 0;
  for slot := 1 to 28 do
    if isItemIn(tabBackpack.getSlotBox(slot)) then
      incEx(result, 1);
  {$IFDEF DEBUG}print('getInvCount(): '+toStr(result)+';', TDebug.FOOTER);{$ENDIF}
end;

function n_dropItems: boolean;
var
  i, amt, tmpTime: integer;
  sBox: TBox;
  pts: TPointArray;
  point: TPoint;
begin
  {$IFDEF DEBUG}print('n_dropItems()', TDebug.HEADER);{$ENDIF}
  amt := getInvCount();
  for i := 1 to 28 do
  begin
    result := getInvCount = 0;
    if result then
      break;
    sBox := tabBackpack.getSlotBox(i);
    if isItemIn(sBox) then
    begin
      mouseBox(sBox, MOUSE_RIGHT);
      if chooseOption.select(['Drop', 'drop', 'rop']) then
        wait(randomRange(100, 350))
      else
      begin
        mouseBox(sBox, MOUSE_RIGHT);
        if chooseOption.select(['Destroy', 'estroy', 'estr', 'stro']) then
          if waitColorCountRange(379903, 0, intToBox(110, 440, 475, 460), 5000, 304, 304) then
          begin
            findColorsTolerance(pts, 1137455, 360, 482, 445, 510, 12);
            mouseBox(pts.getBounds, MOUSE_LEFT);
            tmpTime := getSystemTime+15000;
            while (amt = getInvCount) and (tmpTime > getSystemTime()) do
              wait(30);
          end;
      end;
    end;
  end;
  {$IFDEF DEBUG}print('n_dropItems(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;


function drop(): boolean;
var
  time: integer;
begin
  result := random(5) < 3; // So we don't behave like a bot, more human-like
  if result and getInvCount(true) < 2+random(3) then
    exit(true);

  {$IFDEF DEBUG}print('Drop()', TDebug.HEADER);{$ENDIF}
  time := getSystemTime + 30000;
  while (tabBackpack.countDTM(oreDTM) > 0) and (time > getSystemTime()) do
  begin
    sendKeys(players[currentPlayer].strings[_quickKey], 100+random(500), 100+random(500));
    wait(random(30));
  end;
  result := true;
  if not tabBackpack.isEmpty() then //incase quick key doesn't work for some reason
  begin
    findNormalRandoms();
    n_dropItems();
    result := getInvCount() < 5; //sometimes we mine an ore here, so .isEmpty throw false positive
    if not result then
    begin
      {$IFDEF DEBUG}print('Failed to drop Ore');{$ENDIF}
      players[currentPlayer].strings[_reason] := 'Failed to drop Ore';
      {$IFDEF DEBUG}print('Drop(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
      exit(false);
    end;
  end;
  {$IFDEF DEBUG}print('Drop(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

function mainLoop(): boolean;
var
  i, ii, l, ll, iCount, timer, timeout: integer;
begin
  {$IFDEF DEBUG}print('mainLoop()', TDebug.HEADER);{$ENDIF}
  if not isLoggedIn then
  begin
    {$IFDEF DEBUG}print('mainLoop(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
    players[currentPlayer].strings[_reason] := 'Not logged in';
    exit(false);
  end;

  iCount := getInvCount();
  timeout := getSystemTime() + (3*60000); //set 3 minute timer
  while timeout > getSystemTime() do
    if data.findColorSpiral(mainscreen.getBounds()) then
    begin
      timer := getSystemTime+(_waitTime+random(round(_waitTime*0.25)));
      if drop() then //Should ALWAYS result true unless fatal error
      begin
        minimap.waitFlag();
        while timer > getSystemTime() do
          if iCount <> getInvCount() then
          begin
            inc(players[currentPlayer].integers[_count]); // not 100% accurate
            break;
          end else
           antiban();
        printProgress;
      end else
      begin
        {$IFDEF DEBUG}print('Failed while dropping ore', TDebug.WARNING);{$ENDIF}
        {$IFDEF DEBUG}print('mainLoop(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
        exit(false);
      end;
      result := true;
      break;
    end;
  {$IFDEF DEBUG}print('mainLoop(): '+toStr(result), TDebug.FOOTER);{$ENDIF}
end;

var
  t, tries: integer;
begin
  if not initScript() then
    terminateScript;

  _antibanTime := getSystemTime() + ((untilAntiban+random(round(untilAntiban*0.5)))*60000); // set minimum wait between antiban routines
  t := round(getSystemTime() + players[currentPlayer].integers[_breakIn]*60000+(random(players[currentPlayer].integers[_breakIn]*60000)*0.15));
  while players.getActive() > 0 do
  begin
    while t > getSystemTime() do
      if not mainLoop() then
      begin
        players[currentPlayer].strings[_reason] := 'Failed to mine rock in 3 minutes';
        if not players.next(false, true) then
          print('No more active players. Terminating script.', TDebug.FATAL);
      end;

    if players.getActive > 1 then
      players.next()
    else
      if not takeBreak then
        print('No more active players. Terminating script.', TDebug.FATAL);

    t := round(getSystemTime() + players[currentPlayer].integers[_breakIn]*60000+(random(players[currentPlayer].integers[_breakIn]*60000)*0.15));
    if not initPlayer then
      if not players.next(false, true) then
        print('No more active players. Terminating script.', TDebug.FATAL);
  end;
end.
